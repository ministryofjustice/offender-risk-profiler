import org.springframework.boot.gradle.plugin.SpringBootPlugin

import java.time.Instant
import java.time.LocalDate

plugins {
    id 'java'
    id 'org.springframework.boot' version '2.2.7.RELEASE'
    id 'io.spring.dependency-management' version '1.0.9.RELEASE'
    id 'org.owasp.dependencycheck' version '5.3.2.1'
    id 'com.github.ben-manes.versions' version '0.28.0'
}

def isNonStable = { String version ->
    def stableKeyword = ['RELEASE', 'FINAL', 'GA'].any { it -> version.toUpperCase().contains(it) }
    def regex = /^[0-9,.v-]+(-r)?$/
    return !stableKeyword && !(version ==~ regex)
}

dependencyUpdates {
    rejectVersionIf {
        isNonStable(it.candidate.version) && !isNonStable(it.currentVersion)
    }
}

repositories {
    mavenLocal()
    mavenCentral()
}

configurations {
  agentDeps
  agentDeps.transitive = false
}

project.sourceCompatibility = 11
project.targetCompatibility = 11

dependencyCheck {
    failBuildOnCVSS = 5
    suppressionFiles = ['dependency-check-suppress-spring.xml']
    format = 'ALL'
    analyzers {
        assemblyEnabled = false
    }
}

group = 'uk.gov.justice.digital.hmpps'

def today = Instant.now()
def todaysDate = LocalDate.now().format('yyyy-MM-dd')
version = System.getenv('CI') ? "${todaysDate}.${System.getenv('CIRCLE_BUILD_NUM')}" : todaysDate

jar {
    archiveBaseName = rootProject.name
    manifest {
        attributes 'Implementation-Title': rootProject.name, 'Implementation-Version': archiveVersion
    }
}

springBoot {
    buildInfo {
        properties {
            artifact = rootProject.name
            version
            group
            name = rootProject.name
            time = today
            additional = [
                    by                   : System.properties['user.name'],
                    operatingSystem      : "${System.properties['os.name']} (${System.properties['os.version']})",
                    continuousIntegration: System.getenv('CI') ? true : false,
                    machine              : InetAddress.localHost.hostName
            ]
        }
    }
}

dependencyManagement {
    imports {
        mavenBom(SpringBootPlugin.BOM_COORDINATES)
    }
}

configurations {
    compile.exclude  module: "tomcat-jdbc"
}

dependencies {
    annotationProcessor "org.springframework.boot:spring-boot-configuration-processor"
    annotationProcessor('org.projectlombok:lombok:1.18.12')

    runtimeOnly 'com.h2database:h2:1.4.200'
    runtimeOnly 'org.flywaydb:flyway-core:6.4.2'
    runtimeOnly 'org.postgresql:postgresql:42.2.12'

    implementation("org.springframework.boot:spring-boot-starter-web")
    implementation("org.springframework.boot:spring-boot-starter-actuator")
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation("org.springframework.boot:spring-boot-starter-cache")

    // NOTE spring-boot-devtools does not currently play nicely with spring-data-redis,
    // see https://github.com/spring-projects/spring-boot/issues/11822, which claims to be fixed but is not.
    implementation 'org.springframework.data:spring-data-redis'
    implementation 'redis.clients:jedis:3.3.0'

    implementation("org.springframework.cloud:spring-cloud-starter-aws-messaging:2.2.1.RELEASE")
    implementation 'org.springframework:spring-jms:5.2.6.RELEASE'
    implementation 'com.amazonaws:amazon-sqs-java-messaging-lib:1.0.8'

    implementation("org.springframework.boot:spring-boot-starter-security")
    implementation("org.springframework.security.oauth:spring-security-oauth2:2.4.1.RELEASE")
    implementation("org.springframework.security:spring-security-jwt:1.1.0.RELEASE")

    implementation 'org.apache.camel:camel-spring-boot:2.25.1'
    implementation 'org.apache.camel:camel-csv:2.25.1'
    implementation ('org.apache.camel:camel-aws:2.25.1') {
        exclude group: 'com.amazonaws', module: 'aws-java-sdk-sagemaker'
        exclude group: 'com.amazonaws', module: 'aws-java-sdk-sagemakerruntime'
        exclude group: 'com.amazonaws', module: 'aws-java-sdk-opsworkscm'
        exclude group: 'com.amazonaws', module: 'aws-java-sdk-eks'
    }
    implementation 'net.javacrumbs.shedlock:shedlock-spring:4.10.1'
    implementation 'net.javacrumbs.shedlock:shedlock-provider-jdbc-template:4.10.1'

    implementation 'net.logstash.logback:logstash-logback-encoder:6.3'
    implementation 'com.microsoft.azure:applicationinsights-spring-boot-starter:2.6.0'
    implementation 'com.microsoft.azure:applicationinsights-logging-logback:2.6.0'
    agentDeps 'com.microsoft.azure:applicationinsights-agent:2.6.0'

    implementation 'javax.annotation:javax.annotation-api:1.3.2'
    implementation 'javax.xml.bind:jaxb-api:2.3.1'
    implementation 'com.sun.xml.bind:jaxb-impl:2.3.3'
    implementation 'com.sun.xml.bind:jaxb-core:2.3.0.1'
    implementation 'javax.activation:activation:1.1.1'
    implementation('javax.transaction:javax.transaction-api:1.3')

    implementation 'io.springfox:springfox-swagger2:2.9.2'
    implementation 'io.springfox:springfox-swagger-ui:2.9.2'

    implementation "io.jsonwebtoken:jjwt:0.9.1"
    implementation 'com.github.timpeeters:spring-boot-graceful-shutdown:2.2.2'

    implementation("net.sf.ehcache:ehcache:2.10.6")
    implementation("org.apache.commons:commons-lang3:3.10")
    implementation("org.apache.commons:commons-text:1.8")
    implementation("com.fasterxml.jackson.datatype:jackson-datatype-jsr310:2.11.0")
    implementation("com.pauldijou:jwt-core_2.11:4.3.0")
    implementation 'com.google.guava:guava:29.0-jre'

    compileOnly('org.projectlombok:lombok:1.18.12')

    testAnnotationProcessor 'org.projectlombok:lombok:1.18.12'
    testCompileOnly 'org.projectlombok:lombok:1.18.12'
    testImplementation("org.springframework.boot:spring-boot-starter-test")
    testImplementation("org.assertj:assertj-core:3.16.1")
    testImplementation 'io.github.http-builder-ng:http-builder-ng-apache:1.0.4'

    testImplementation 'org.apache.camel:camel-test-spring:2.25.1'

    compile configurations.agentDeps
}

task copyAgent(type: Copy) {
    from configurations.agentDeps
    into "$buildDir/libs"
}

assemble.dependsOn copyAgent
