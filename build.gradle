import org.springframework.boot.gradle.plugin.SpringBootPlugin

import java.time.Instant
import java.time.LocalDate
import java.time.format.DateTimeFormatter

plugins {
    id 'java'
    id 'groovy'
    id 'org.springframework.boot' version '2.1.6.RELEASE'
    id 'io.spring.dependency-management' version '1.0.8.RELEASE'
    id 'com.github.ben-manes.versions' version '0.21.0'
}

project.sourceCompatibility = 11
project.targetCompatibility = 11

def applicationName = 'offender-risk-profiler'
def today = Instant.now()
def todayDate = LocalDate.now()
def formatter = DateTimeFormatter.ofPattern("yyyy-MM-dd")
group = 'uk.gov.justice.digital.hmpps'
version todayDate.format(formatter)

jar {
    archiveBaseName.set(applicationName)
    manifest {
        attributes 'Implementation-Title': applicationName,
                'Implementation-Version': System.getenv('CI') ? "${version}.${System.getenv('CIRCLE_BUILD_NUM')}" : version
    }
}

springBoot {
    buildInfo {
        properties {
            artifact = rootProject.name
            version = System.getenv('CI') ? "${version}.${System.getenv('CIRCLE_BUILD_NUM')}" : version
            group = 'uk.gov.justice.digital.hmpps'
            name = rootProject.name
            time = today
            additional = [
                    by                   : System.properties['user.name'],
                    operatingSystem      : "${System.properties['os.name']} (${System.properties['os.version']})",
                    continuousIntegration: System.getenv('CI') ? true : false,
                    machine              : InetAddress.localHost.hostName
            ]
        }
    }
}

repositories {
    mavenLocal()
    mavenCentral()
}

dependencyManagement {
    imports {
        mavenBom(SpringBootPlugin.BOM_COORDINATES)
    }
}

configurations {
    compile.exclude  module: "tomcat-jdbc"
}

dependencies {
    annotationProcessor "org.springframework.boot:spring-boot-configuration-processor"
    annotationProcessor('org.projectlombok:lombok:1.18.8')

    runtime 'com.h2database:h2:1.4.199'
    runtime 'org.flywaydb:flyway-core:5.2.4'
    runtime 'org.postgresql:postgresql:42.2.6'

    implementation("org.springframework.boot:spring-boot-starter-web")
    implementation("org.springframework.boot:spring-boot-devtools")
    implementation("org.springframework.boot:spring-boot-starter-actuator")
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation("org.springframework.boot:spring-boot-starter-cache")

    implementation("org.springframework.boot:spring-boot-starter-security")
    implementation("org.springframework.security.oauth:spring-security-oauth2:2.3.6.RELEASE")
    implementation("org.springframework.security:spring-security-jwt:1.0.10.RELEASE")

    implementation 'org.apache.camel:camel-spring-boot:2.23.2'
    implementation 'org.apache.camel:camel-csv:2.23.2'
    implementation 'org.apache.camel:camel-aws:2.23.2'
    implementation 'net.javacrumbs.shedlock:shedlock-spring:2.5.0'
    implementation 'net.javacrumbs.shedlock:shedlock-provider-jdbc-template:2.5.0'

    implementation 'net.logstash.logback:logstash-logback-encoder:6.1'
    implementation 'com.microsoft.azure:applicationinsights-spring-boot-starter:1.2.0-BETA'
    implementation 'com.microsoft.azure:applicationinsights-logging-logback:2.4.0-BETA'

    implementation 'javax.annotation:javax.annotation-api:1.3.2'
    implementation 'javax.xml.bind:jaxb-api:2.3.0'
    implementation 'com.sun.xml.bind:jaxb-impl:2.3.2'
    implementation 'com.sun.xml.bind:jaxb-core:2.3.0.1'
    implementation 'javax.activation:activation:1.1.1'
    implementation('javax.transaction:javax.transaction-api:1.3')

    implementation 'io.springfox:springfox-swagger2:2.9.2'
    implementation 'io.springfox:springfox-swagger-ui:2.9.2'

    implementation("io.jsonwebtoken:jjwt:0.9.1")
    implementation 'com.github.timpeeters:spring-boot-graceful-shutdown:2.1.1'

    implementation("net.sf.ehcache:ehcache:2.10.6")
    implementation("org.apache.commons:commons-lang3:3.9")
    implementation("org.apache.commons:commons-text:1.6")
    implementation("com.fasterxml.jackson.datatype:jackson-datatype-jsr310:2.9.9")
    implementation("com.pauldijou:jwt-core_2.11:2.1.0")

    compileOnly('org.projectlombok:lombok:1.18.8')

    testImplementation("org.springframework.boot:spring-boot-starter-test")
    testImplementation("org.assertj:assertj-core:3.12.2")
    testImplementation 'org.codehaus.groovy:groovy-all:2.5.4'
    testImplementation 'org.spockframework:spock-spring:1.3-groovy-2.5'
    testImplementation('org.spockframework:spock-core:1.3-groovy-2.5') {
        exclude group: "org.codehaus.groovy"
    }

    testImplementation 'org.gebish:geb-core:3.0.1'
    testImplementation 'org.gebish:geb-spock:3.0.1'
    testImplementation 'org.seleniumhq.selenium:selenium-support:3.141.59'
    testImplementation 'org.seleniumhq.selenium:selenium-chrome-driver:3.141.59'
    testImplementation 'org.seleniumhq.selenium:selenium-firefox-driver:3.141.59'
    testImplementation 'io.github.http-builder-ng:http-builder-ng-apache:1.0.3'

    testImplementation 'com.github.tomakehurst:wiremock-standalone:2.23.2'
    testImplementation 'com.github.tomjankes:wiremock-groovy:0.2.0'
    testImplementation 'org.apache.camel:camel-test-spring:2.23.2'
}
